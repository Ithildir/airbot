buildscript {
	dependencies {
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins.source.formatter", version: "latest.release"
	}

	repositories {
		jcenter()
	}
}

apply plugin: "application"
apply plugin: "com.liferay.source.formatter"
apply plugin: "jacoco"

group = "com.github.ithildir"
mainClassName = "io.vertx.core.Launcher"
sourceCompatibility = 1.8

check {
	dependsOn checkSourceFormatting
}

dependencies {
	compile group: "io.vertx", name: "vertx-core", version: "3.4.1"
	compile group: "io.vertx", name: "vertx-service-proxy", version: "3.4.1"
	compile group: "org.apache.commons", name: "commons-lang3", version: "3.5"

	compileOnly classifier: "processor", group: "io.vertx", name: "vertx-service-proxy", version: "3.4.1"
	compileOnly group: "io.vertx", name: "vertx-codegen", version: "3.4.1"

	testCompile group: "io.vertx", name: "vertx-unit", version: "3.4.1"
	testCompile group: "junit", name: "junit", version: "4.12"
}

jacocoTestReport {
	doFirst {
		classDirectories = files(classDirectories.files.collect {
			fileTree(dir: it, excludes: ["**/*VertxEBProxy*", "**/*VertxProxyHandler*"])
		})
	}

	reports {
		xml.enabled = true
	}
}

jar {
	exclude "**/*.java"
}

repositories {
	jcenter()
}

run {
	args "run", "com.github.ithildir.airbot.server.AirBotServerVerticle"
	args "-conf", "configuration.json"

	systemProperty "vertx.disableDnsResolver", true
}