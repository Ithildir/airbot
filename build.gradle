buildscript {
	dependencies {
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins.source.formatter", version: "latest.release"
	}

	repositories {
		jcenter()
	}
}

apply plugin: "application"
apply plugin: "com.liferay.source.formatter"
apply plugin: "jacoco"

task runVertxCodeGen(type: JavaCompile)

group = "com.github.ithildir"
mainClassName = "io.vertx.core.Launcher"
sourceCompatibility = 1.8

def generatedSrcDir = file("src/main/generated")

check {
	dependsOn checkSourceFormatting
}

compileJava {
	dependsOn runVertxCodeGen
}

dependencies {
	compile group: "ai.api", name: "libai", version: "1.6.12"
	compile group: "io.vertx", name: "vertx-config", version: "3.4.2"
	compile group: "io.vertx", name: "vertx-core", version: "3.4.2"
	compile group: "io.vertx", name: "vertx-service-proxy", version: "3.4.2"
	compile group: "io.vertx", name: "vertx-web", version: "3.4.2"
	compile group: "io.vertx", name: "vertx-web-client", version: "3.4.2"
	compile group: "org.apache.commons", name: "commons-lang3", version: "3.6"
	compile group: "org.ocpsoft.prettytime", name: "prettytime", version: "4.0.1.Final"
	compile group: "org.slf4j", name: "slf4j-jdk14", version: "1.7.25"

	compileOnly group: "io.vertx", name: "vertx-codegen", version: "3.4.2"

	testCompile group: "io.vertx", name: "vertx-unit", version: "3.4.2"
	testCompile group: "junit", name: "junit", version: "4.12"
}

jacocoTestReport {
	doFirst {
		classDirectories = files(classDirectories.files.collect {
			fileTree(dir: it, excludes: ["**/*VertxEBProxy*", "**/*VertxProxyHandler*"])
		})
	}

	reports {
		xml.enabled = true
	}
}

repositories {
	jcenter()

	maven {
		url "https://jitpack.io"
	}
}

run {
	args "run", "com.github.ithildir.airbot.AirBotVerticle"

	doFirst {
		def airbotEnvironment = [:]

		environment.each {
			if (it.key.startsWith("airbot_")) {
				airbotEnvironment[it.key.substring(7)] = it.value
			}
		}

		environment << airbotEnvironment
	}
}

runVertxCodeGen {
	classpath = configurations.compileClasspath
	description = "Runs the Vert.x code generator."
	destinationDir = generatedSrcDir
	group = "build"

	options.compilerArgs = [
		"-Acodegen.output=${destinationDir.absolutePath}",
		"-proc:only",
		"-processor", "io.vertx.codegen.CodeGenProcessor"
	]

	source = sourceSets.main.java
}

sourceSets {
	main {
		java {
			srcDirs generatedSrcDir
		}
	}
}